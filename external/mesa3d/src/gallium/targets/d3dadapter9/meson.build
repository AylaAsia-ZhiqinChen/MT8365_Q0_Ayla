# Copyright © 2017 Dylan Baker
# Copyright © 2018 Intel Corporation

# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:

# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

# TODO: support non-static targets
# Static targets are always enabled in autotools (unless you modify
# configure.ac)

nine_version = ['1', '0', '0']

gallium_nine_c_args = [
  '-DGALLIUM_DDEBUG',
  '-DGALLIUM_RBUG',
  '-DGALLIUME_TRACE',
]
gallium_nine_ld_args = []
gallium_nine_link_depends = []

if with_ld_version_script
  gallium_nine_ld_args += ['-Wl,--version-script', join_paths(meson.current_source_dir(), 'd3dadapter9.sym')]
  gallium_nine_link_depends += files('d3dadapter9.sym')
endif

libgallium_nine = shared_library(
  'd3dadapter9',
  [files('description.c', 'getproc.c', 'drm.c'), xmlpool_options_h],
  include_directories : [
    inc_include, inc_src, inc_loader, inc_mapi, inc_mesa, inc_util,
    inc_dri_common, inc_gallium, inc_gallium_aux, inc_gallium_winsys,
    inc_gallium_drivers, inc_d3d9,
    include_directories('../../state_trackers/nine'),
  ],
  c_args : [c_vis_args, gallium_nine_c_args],
  cpp_args : [cpp_vis_args],
  link_args : [ld_args_gc_sections, gallium_nine_ld_args],
  link_depends : gallium_nine_link_depends,
  link_with : [
    libgalliumvl_stub, libgallium, libnine_st, libmesa_util, libddebug,
    librbug, libtrace, libpipe_loader_static, libws_null, libwsw, libswdri,
    libswkmsdri,
  ],
  dependencies : [
    dep_selinux, dep_expat, dep_libdrm, dep_llvm,
    driver_swrast, driver_r300, driver_r600, driver_radeonsi, driver_nouveau,
    driver_i915, driver_svga,
  ],
  name_prefix : '',
  version : '.'.join(nine_version),
  install : true,
  install_dir : d3d_drivers_path,
)

pkg.generate(
  name : 'd3d',
  description : 'Native D3D driver modules',
  version : '.'.join(nine_version),
  requires_private : 'libdrm >= 2.4.75',
  variables : ['moduledir=${prefix}/@0@'.format(d3d_drivers_path)],
)
